apply plugin: "java"
apply plugin: "application"
apply plugin: "distribution"

mainClassName = "org.openremote.manager.Main"

dependencies {

    compile resolveProject(":container")
    compile resolveProject(":agent")
    compile resolveProject(":model")

    compile "org.xerial:sqlite-jdbc:$sqliteVersion"

    compile "org.mnode.ical4j:ical4j:$ical4jVersion"

    compile "net.jafama:jafama:$jafamaVersion"
    compile "org.geotools:gt-main:$geotoolsVersion"
    compile "com.google.firebase:firebase-admin:$firebaseAdminVersion"
    compile "org.simplejavamail:simple-java-mail:$simpleJavaMailVersion"
    compile "io.swagger.core.v3:swagger-jaxrs2:$swaggerVersion"

    compile ("io.netty:netty-codec-http:$nettyVersion") {
        force = true
    }

    compile ("io.moquette:moquette-broker:$moquetteVersion") {
        exclude module: "slf4j-log4j12" // Don't want log4J
    }

    compile("org.quartz-scheduler:quartz:$quartzVersion") {
        exclude group: "c3p0"
    }
}

// Load version properties file and use same version as source code
def props = new Properties()
file("src/main/resources/system.properties").withInputStream { props.load(it) }
version = props.get("version")

jar {
    archivesBaseName = "openremote-${project.name}"
}

distributions {
    main {
        contents {
            from(project.projectDir) {
                include("*Dockerfile")
            }
            into ("web") {
                from("${rootDir}/LICENSE.txt")
            }
            into ("web") {
                from("${projectDir}/src/web")
            }
            into ("map") {
                from("${projectDir}/src/map")
            }
        }
    }
}
